# sdl3_ttf cmake project-config input for ./configure scripts

include(FeatureSummary)
set_package_properties(SDL3_ttf PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_ttf/"
    DESCRIPTION "Support for TrueType (.ttf) font files with Simple Directmedia Layer"
)

set(SDL3_ttf_FOUND TRUE)

set(SDL3TTF_HARFBUZZ @TTF_USE_HARFBUZZ@)
set(SDL3TTF_FREETYPE TRUE)

set(SDL3TTF_VENDORED @SDL3TTF_VENDORED@)

set(SDL3TTF_SDL3_REQUIRED_VERSION @SDL_VERSION@)

get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}/@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl3ttf_extra_static_libraries "@TTF_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl3ttf_extra_static_libraries}" _sdl3ttf_extra_static_libraries)

set(_sdl3ttf_bindir   "${bindir}")
set(_sdl3ttf_libdir   "${libdir}")
set(_sdl3ttf_incdir   "${includedir}/SDL3")

# Convert _sdl3ttf_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl3ttf_extra_static_libraries "${_sdl3ttf_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl3ttf_extra_static_libraries "${_sdl3ttf_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl3ttf_extra_static_libraries "${_sdl3ttf_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL3_ttf::SDL3_ttf)
    if(WIN32)
        set(_sdl3ttf_dll "${_sdl3ttf_bindir}/SDL3_ttf.dll")
        set(_sdl3ttf_imp "${_sdl3ttf_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_ttf.dll${CMAKE_STATIC_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl3ttf_dll}" AND EXISTS "${_sdl3ttf_imp}")
            add_library(SDL3_ttf::SDL3_ttf SHARED IMPORTED)
            set_target_properties(SDL3_ttf::SDL3_ttf
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl3ttf_dll}"
                    IMPORTED_IMPLIB "${_sdl3ttf_imp}"
            )
        endif()
        unset(_sdl3ttf_dll)
        unset(_sdl3ttf_imp)
    else()
        set(_sdl3ttf_shl "${_sdl3ttf_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL3_ttf${CMAKE_SHARED_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl3ttf_shl}")
            add_library(SDL3_ttf::SDL3_ttf SHARED IMPORTED)
            set_target_properties(SDL3_ttf::SDL3_ttf
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl3ttf_shl}"
            )
        endif()
    endif()
    if(TARGET SDL3_ttf::SDL3_ttf)
        set_target_properties(SDL3_ttf::SDL3_ttf
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl3ttf_incdir}"
                COMPATIBLE_INTERFACE_BOOL "SDL3_SHARED"
                INTERFACE_SDL3_SHARED "ON"
        )
    endif()
endif()

if(NOT TARGET SDL3_ttf::SDL3_ttf-static)
    set(_sdl3ttf_stl "${_sdl3ttf_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_ttf${CMAKE_STATIC_LIBRARY_SUFFIX}")
    if(EXISTS "${_sdl3ttf_stl}")
        add_library(SDL3_ttf::SDL3_ttf-static STATIC IMPORTED)
        set_target_properties(SDL3_ttf::SDL3_ttf-static
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl3ttf_incdir}"
                IMPORTED_LOCATION "${_sdl3ttf_stl}"
                INTERFACE_LINK_LIBRARIES "${_sdl3ttf_extra_static_libraries}"
        )
    endif()
    unset(_sdl3ttf_stl)
endif()

unset(_sdl3ttf_extra_static_libraries)
unset(_sdl3ttf_bindir)
unset(_sdl3ttf_libdir)
unset(_sdl3ttf_incdir)
